    !include "MUI2.nsh"
	!include ".\version.nsh"
	!include ".\xlw.nsh"
	!include "LogicLib.nsh"

;------------------------------------------------------------------------------------------------------------------------
;------------------------------------------------------------------------------------------------------------------------
;Definitions

    !define APP "xlwTemplateExtractor"
	Name "${APP}"
    OutFile "${APP}.exe"
	RequestExecutionLevel user
	BrandingText "${APP}"
	InstallDir "$DOCUMENTS\XLL_Project"

;------------------------------------------------------------------------------------------------------------------------
;------------------------------------------------------------------------------------------------------------------------
;Interface Settings

	!define MUI_PAGE_HEADER_TEXT "Select Installation Directory"
	!define MUI_PAGE_HEADER_SUBTEXT "Select the directory where the template project will be placed"
	!define MUI_DIRECTORYPAGE_TEXT_TOP "When the project is extracted, you should start by changing the name of the project folder and any relevant project files"
    !define MUI_ABORTWARNING
    !define MUI_ICON "xlw\docs\images\xlw_32x32.ico"
    !define MUI_UNICON "xlw\docs\images\xlw_32x32.ico"
	!define MUI_HEADERIMAGE

	!define MUI_HEADERIMAGE_BITMAP "xlw\docs\images\logo.bmp"
	
;------------------------------------------------------------------------------------------------------------------------
;------------------------------------------------------------------------------------------------------------------------
;Pages

	Page custom ExtractorPage ;Custom page

    !insertmacro MUI_PAGE_DIRECTORY
    !insertmacro MUI_PAGE_INSTFILES
	
;------------------------------------------------------------------------------------------------------------------------
;------------------------------------------------------------------------------------------------------------------------
;Languages

    !insertmacro MUI_LANGUAGE "English"
	
;------------------------------------------------------------------------------------------------------------------------
;------------------------------------------------------------------------------------------------------------------------

;------------------------------------------------------------------------------------------------------------------------
;------------------------------------------------------------------------------------------------------------------------

Var DIALOG
	
Var VC14
Var VC12
Var VC11
Var VC10
Var CODEBLOCKS
Var GCC

Var VC14_STATE
Var VC12_STATE
Var VC11_STATE
Var VC10_STATE
Var CODEBLOCKS_STATE
Var GCC_STATE

Function ExtractorPage

	!insertmacro MUI_HEADER_TEXT "XLW Template Extractor"  "Select the Dev environment for which the template project is to be extracted"

	GetDlgItem $0 $HWNDPARENT 1
	EnableWindow $0 0

	nsDialogs::Create 1018
	Pop $DIALOG

	GetFunctionAddress $0 RadioChanged
	
    ${NSD_CreateRadioButton} 100 30 100% 10% "Visual C++ 2015 (VC14)"    
	Pop $VC14
	nsDialogs::OnClick  $VC14 $0
    ${NSD_CreateRadioButton} 100 30 100% 10% "Visual C++ 2013 (VC12)"    
	Pop $VC12
	nsDialogs::OnClick  $VC12 $0
    ${NSD_CreateRadioButton} 100 30 100% 10% "Visual C++ 2012 (VC11)"    
	Pop $VC11
	nsDialogs::OnClick  $VC11 $0
	${NSD_CreateRadioButton} 100 60 100% 10% "Visual C++ 2010 (VC10)"    
	Pop $VC10
	nsDialogs::OnClick  $VC10 $0
	${NSD_CreateRadioButton} 100 180 100% 10% "Code::Blocks(MingW)"
	Pop $CODEBLOCKS
	nsDialogs::OnClick  $CODEBLOCKS $0
	${NSD_CreateRadioButton} 100 210 100% 10% "GCC/Make"
	Pop $GCC
	nsDialogs::OnClick  $GCC $0
	
	nsDialogs::Show

FunctionEnd

Function RadioChanged
	Pop $0 # dir hwnd

	GetDlgItem $0 $HWNDPARENT 1
	EnableWindow $0 1

	${NSD_GetState} $VC14 $VC14_STATE
	${NSD_GetState} $VC12 $VC12_STATE
	${NSD_GetState} $VC11 $VC11_STATE
	${NSD_GetState} $VC10 $VC10_STATE
	${NSD_GetState} $CODEBLOCKS $CODEBLOCKS_STATE
	${NSD_GetState} $GCC $GCC_STATE
	
FunctionEnd

!macro projectfiles dir
		SetOutPath "$INSTDIR"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.vcxproj"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.vcxproj.filters"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.sln"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.mak"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.nmake"
		File /nonfatal /r "xlw\Template_Projects\${dir}\Makefile"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.cbp"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.workspace"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.bat"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.cpp"
		File /nonfatal /r "xlw\Template_Projects\${dir}\*.h"
		CreateDirectory  "$INSTDIR\AutoGeneratedSource"

	!macroend
	
!macro GETDIR  STATEFLAG TDIR

	${If} ${STATEFLAG} ==  ${BST_CHECKED}
			!insertmacro projectfiles ${TDIR}
			 Exec "explorer.exe $INSTDIR" 
	${Endif}

!macroend

Section #
	!insertmacro GETDIR $VC14_STATE "vc14"
	!insertmacro GETDIR $VC12_STATE "vc12"
	!insertmacro GETDIR $VC11_STATE "vc11"
	!insertmacro GETDIR $VC10_STATE "vc10"
	!insertmacro GETDIR $CODEBLOCKS_STATE "codeblocks-gcc"
	!insertmacro GETDIR $GCC_STATE "gcc-make"

SectionEnd 










